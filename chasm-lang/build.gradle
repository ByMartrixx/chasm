plugins {
    id 'java'
    id 'antlr'
    id 'checkstyle'
}

group 'org.quiltmc.chasm'
version '1.0-SNAPSHOT'

sourceSets {
    testData {
        java {
            compileClasspath = sourceSets.main.output + configurations.testCompileClasspath
            runtimeClasspath = sourceSets.main.output + project.configurations.testRuntimeClasspath
        }
    }
}

compileJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileTestJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileTestDataJava {
    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

testClasses.dependsOn("testDataClasses")

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

checkstyle {
    toolVersion "9.1"
    ignoreFailures(false)
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':chasm')
    implementation 'org.ow2.asm:asm:9.2'


    antlr 'org.antlr:antlr4:4.9.3'

    testImplementation 'org.ow2.asm:asm-util:9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}